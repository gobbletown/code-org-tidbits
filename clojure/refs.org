A ref is like a mutable pointer. It can point
to different immutable values over time.
Further, Clojure makes sure that if multiple
threads are accessing the same ref they won't
interfere with each other.g

There is a function called ref-set that allows
a programmer to change what a ref points to. A
ref is mutable but the data it points to is
always immutable.

** transaction
+ transaction :: When two or more refs need to
    be updated in a coordinated operation a
    mechanism is required to make sure that all of
    the ref updates happen or none of them do.
    This is what a transaction does and Clojure
    supports them.

transaction
    Particularly powerful in multi-threaded
    programs where coordinated access to
    multiple global refs is required.

** basic example
#+BEGIN_SRC clojure -n :i clj :async :results verbatim code
  (def quarters-in-the-bank (ref 4000)) ;$1000
  (def dollars-in-the-bank (ref 1000)) ;$1000
#+END_SRC

** =deref= (shorthand: =@=)
To retrieve a value of a ref there is a
function called deref.

This is short for 'dereference' which is a
term used in programming languages with
explicit pointers.

This says, "go to what the ref points to and
return that value".

The values can be used like other values as
long as you dereference them.

** =dosync=
Whenever two refs need coordination in order
to change together the operations need to be
performed in a transaction.

+ A _transaction_ is a group of operations that are:
  - =atomic=
    all ref updates will appear to happen
    simultaneously.
  - =consistent=
    all ref updates can be validated for
    correctness and all of them rolled back if
    any one fails.
  - =isolated=
    no other part of the program can see
    partially updated refs from other
    transactions.

*** =dosync= creates a transaction

** Final code
#+BEGIN_SRC clojure -n :i clj :async :results verbatim code
  (import '(java.util.concurrent Executors))

  (def quarters-in-the-bank (ref 4000)) ;$1000
  (def dollars-in-the-bank (ref 1000)) ;$1000

  (defn print-bank-state []
      (println)
      (println "========================== Bank State ==========================")
      (println "Currently, there are" (deref dollars-in-the-bank) "dollars and" (deref quarters-in-the-bank) "quarters in the bank")
      (println "The dollar value of the bank is" (+ (deref dollars-in-the-bank) (/ (deref quarters-in-the-bank) 4)))
      (println "================================================================")
      (println))

  (defn exchange-dollars-for-quarters [dollars-to-add]
      (let [quarters-to-remove (* dollars-to-add 4)]
          ;(println "Exchanging" dollars-to-add "dollars for" quarters-to-remove "quarters")
          ;(println "(Adding" dollars-to-add "dollars and removing" quarters-to-remove "quarters)")
          (dosync
              (ref-set dollars-in-the-bank (+ @dollars-in-the-bank dollars-to-add))
              (ref-set quarters-in-the-bank (- @quarters-in-the-bank quarters-to-remove)))))

  (defn exchange-quarters-for-dollars [quarters-to-add]
      (let [dollars-to-remove (/ quarters-to-add 4)]
          ;(println "Exchanging" quarters-to-add "quarters for" dollars-to-remove "dollars")
          ;(println "(Adding" quarters-to-add "quarters and removing" dollars-to-remove "dollars)")
          (dosync
              (ref-set quarters-in-the-bank (+ @quarters-in-the-bank quarters-to-add))
              (ref-set dollars-in-the-bank (- @dollars-in-the-bank dollars-to-remove)))))

  (defn random-true-false []
      (if (< (Math/random) 0.5) true false))

  (defn random-exchange []
      (Thread/sleep (* (Math/random) 50.0))
      (if (random-true-false) (exchange-dollars-for-quarters 1) (exchange-quarters-for-dollars 4)))

  (defn exchange-many-times-in-threads [number-of-threads]
      (let [pool (Executors/newFixedThreadPool number-of-threads)
            tasks (repeat number-of-threads random-exchange)]
          (.invokeAll pool tasks)))

  (print-bank-state)
  (exchange-many-times-in-threads 1000)
  (print-bank-state)

  ; (print-bank-state)
  ; (exchange-dollars-for-quarters 10)
  ; (print-bank-state)
  ; (exchange-quarters-for-dollars 20)
  ; (print-bank-state)
#+END_SRC